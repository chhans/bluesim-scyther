const pk: Function;
secret sk: Function;
inversekeys (pk,sk);
secret k: Function;

hashfunction hash;
usertype UID, Timestamp, Amount;
const a: Amount;

macro IDt = hash(a, r, IDc, IDm);
macro Sm = {hash(C, IDm, a, r)}sk(M);
macro Sc = {hash(B, IDc, IDm, a, r, Sm)}sk(C);
macro Sb = {hash(M, C, IDt, a, r, t)}sk(B);

protocol EncryptionProtocol1(M, C, B)
{
	role M
	{
		fresh r: Nonce;
		fresh IDm: UID;
		var IDc, IDm: UID;
		var t: Timestamp;

		/*
		* Send unencrypted transaction request to customer
		*/
		send_1 (M, C, IDm, a, r, Sm);

		/*
		* Receive encrypted payment confirmation from bank
		*/
		recv_3 (B,M, { IDt, a, r, t, Sb }k(M,B) );

		claim (M, Alive);
		claim (M, Nisynch);
		claim (M, Niagree);
	}

	role C
	{
		fresh IDc: UID;
		var r: Nonce;
		var IDm: UID;
		var t: Timestamp;

		/*
		* Receive unencrypted transaction request from merchant
		*/
		recv_1 (M, C, IDm, a, r, Sm);

		/*
		* Send encrypted transaction confirmation to bank
		*/
		send_2 (C, B, { IDc, IDm, a, r, Sm, Sc }k(C,B) );

		/*
		* Claim non-injective synch and agreement before receiving the last message to prevent Scyther from flagging redirection of send_3 to C as an attack
		*/
		claim (C, Nisynch);
		claim (C, Niagree);

		/*
		* Receive encrypted payment confirmation from bank
		*/
		recv_4 (B, C, { IDt, a, r, t, Sb }k(C,B) );

		claim (C, Secret, IDc);
		claim (C, Secret, Sc);
		claim (C, Alive);
	}

	role B
	{
		var IDm, IDc: UID;
		var r: Nonce;

		/*
		* Receive encrypted transaction confirmation/payment request from customer
		*/
		recv_2 (C, B, { IDc, IDm, a, r, Sm, Sc }k(C,B) );

		fresh t: Timestamp;

		/*
		* Send encrypted payment confirmations to merchant and customer
		*/
		send_3 (B, M, { IDt, a, r, t, Sb }k(M, B) );
		send_4 (B, C, { IDt, a, r, t, Sb }k(C, B) );

		claim (B, Secret, IDt);
		claim (B, Secret, t);
		claim (B, Secret, Sb);
		claim (B, Alive);
		claim (B, Nisynch);
		claim (B, Niagree);
	}
}