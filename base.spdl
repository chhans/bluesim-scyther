const pk: Function;
secret sk: Function;
inversekeys (pk,sk);

hashfunction hash;
usertype UID, Timestamp, Amount;
const a: Amount;

macro IDt = hash(a, r, IDc, IDm);
macro Sm = {hash(C, IDm, a, r)}sk(M);
macro Sc = {hash(B, IDc, IDm, a, r, Sm)}sk(C);
macro Sb = {hash(M, C, IDt, a, r, t)}sk(B);

protocol BaseCaseProtocol(M, C, B)
{
	role M
	{
		fresh r: Nonce;
		fresh IDm: UID;
		var IDc, IDm: UID;
		var t: Timestamp;

		/*
		* Send transaction request to customer
		*/
		send_1 (M, C, IDm, a, r, Sm);

		/*
		* Receive payment confirmation from bank
		*/
		recv_3 (B,M, IDt, a, r, t, Sb);

		claim (M, Alive);
		claim (M, Nisynch);
		claim (M, Niagree);
	}

	role C
	{
		fresh IDc: UID;
		var r: Nonce;
		var IDm: UID;
		var t: Timestamp;

		/*
		* Receive transaction request from merchant
		*/
		recv_1 (M, C, IDm, a, r, Sm);

		/*
		* Send transaction confirmation to bank
		*/
		send_2 (C, B, IDc, IDm, a, r, Sm, Sc);

		/*
		* Claim non-injective synch and agreement before receiving the last message to prevent Scyther from flagging redirection of send_3 to C as an attack
		*/
		claim (C, Nisynch);
		claim (C, Niagree);

		/*
		* Receive payment confirmation from bank
		*/
		recv_4 (B, C, IDt, a, r, t, Sb);

		claim (C, Alive);
	}

	role B
	{
		var IDm, IDc: UID;
		var r: Nonce;

		/*
		* Receive transaction confirmation/payment request from customer
		*/
		recv_2 (C, B, IDc, IDm, a, r, Sm, Sc);

		fresh t: Timestamp;

		/*
		* Send payment confirmations to merchant and customer
		*/
		send_3 (B, M, IDt, a, r, t, Sb);
		send_4 (B, C, IDt, a, r, t, Sb);

		claim (B, Alive);
		claim (B, Nisynch);
		claim (B, Niagree);
	}
}